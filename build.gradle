plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.3'
    id "org.asciidoctor.jvm.convert" version '3.3.2'
}

configurations {
    asciidoctorExt
}

group = 'com.sports'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.rest-assured:rest-assured'

    implementation 'org.springframework.boot:spring-boot-starter-aop'

    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // JPA & Database
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'mysql:mysql-connector-java:8.0.32'
    runtimeOnly 'com.h2database:h2'

    // flyway
    implementation 'org.flywaydb:flyway-core:9.1.3'
    implementation 'org.flywaydb:flyway-mysql:9.1.3'

    // comment - bad word filter
    implementation 'io.github.vaneproject:badwordfiltering:1.0.0'

    // websocket
    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    // querydsl
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // monitoring
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    // TestContainer
    testImplementation "org.testcontainers:testcontainers:1.19.3"
    testImplementation "org.testcontainers:junit-jupiter:1.19.3"
    testImplementation "org.testcontainers:mysql:1.19.3"

    // rest docs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    // spring security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-test'

    // jwt
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'com.auth0:java-jwt:3.19.1'

    // aws
    implementation 'io.awspring.cloud:spring-cloud-starter-aws:2.4.4'

    // fixture
    testImplementation("com.navercorp.fixturemonkey:fixture-monkey-starter:1.0.21")
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
    testLogging {
        events "failed" // ì‹¤íŒ¨í•œ í…ŒìŠ¤íŠ¸ë§Œ ë¡œê·¸ ì¶œë ¥
        exceptionFormat "full" // Expected vs Actual í¬í•¨
        showStandardStreams = false

        // ì‹¤íŒ¨í•œ í…ŒìŠ¤íŠ¸ì—ì„œë§Œ Expected vs Actualì„ ì¶œë ¥
        afterTest { desc, result ->
            if (result.resultType == TestResult.ResultType.FAILURE) {
                println("ğŸ”´ FAILED TEST: " + desc.getDisplayName())
                println("======================================")
                result.exception?.printStackTrace() // ì‹¤íŒ¨í•œ í…ŒìŠ¤íŠ¸ì˜ ìƒì„¸ ì˜¤ë¥˜ ì¶œë ¥
                println("======================================")
            }
        }
    }
}

asciidoctor {
    configurations 'asciidoctorExt'
    inputs.dir snippetsDir
    dependsOn test
}

tasks.register('copyDocument', Copy) { // 8
    dependsOn asciidoctor

    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")
}

asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')
}

bootJar {
    dependsOn copyDocument
}
